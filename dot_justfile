import ".just/fonts.just"

import ".just/gnome.just"

import ".just/setup.just"

import ".just/ssh.just"

import ".just/starship.just"

# NOTE: Use this file to store shared recipes that can be executed from
# any imported recipe.

[private]
default:
  @just --list

[private]
stderr message level="ERROR":
  #!/usr/bin/env bash

  set -euo pipefail

  MESSAGE="{{ message }}"
  LEVEL="{{ level }}"

  >&2 echo -e "{{ RED + BOLD }}[$LEVEL] {{ NORMAL + BOLD }}${MESSAGE}{{ NORMAL }}"

[private]
stdout message level="":
  #!/usr/bin/env bash

  set -euo pipefail

  MESSAGE="{{ message }}"
  LEVEL="{{ level }}"

  if [ -n "$LEVEL" ]; then
    if [ "$LEVEL" == "WARN" ]; then
      echo -e "{{ YELLOW + BOLD }}[WARN] {{ NORMAL + BOLD }}${MESSAGE}{{NORMAL}}"
    else
      echo "[$LEVEL] $MESSAGE"
    fi
  else
    echo "$MESSAGE"
  fi
